program        → declaration* EOF ;

declaration    → funDecl
               | varDecl
               | statement ;

funDecl        → "fun" function ;
function       → IDENTIFIER "(" paramters? ")" block ;
parameters     → IDENTIFIER ( "," IDENTIFIER )* ;
varDecl        → "var" IDENTIFIER ( "=" expression )? ";" ;

statement      → exprStmt
               | forStmt
               | ifStmt
               | printStmt
               | returnStmt
               | whileStmt
               | breakStmt
               | block ;
exprStmt       → expression ";" ;
forStmt        → "for" "(" ( varDecl | exprStmt | ";" )
                           expression? ";"
                           expression? ")" statement ;
ifStmt         → "if" "(" expression ")" statement ( "else" statement )?
               | "unless" "(" expression ")" statement ;
printStmt      → "print" expression ";" ;
returnStmt     → "return" expression ";" ;
whileStmt      → ( "while" | "until" ) "(" expression ")" statement ;
breakStmt      → "break" ";" ;
block          → "{" declaration* "}" ;

expression     → joined ;
joined         → assignment ( "," assignment )
               | join-error ;
join-error     → "," joined ;
assignment     → IDENTIFIER "=" assignment
               | assign-error
               | logical_or ;
assign-error   → "=" logical_or ;
logical-or     → logical-and ( "or" logical-and )*
               | or-error ;
or-error       → "or" logical-or ;               
logical-and    → equality ( "and" equality )*
               | and-error;
and-error      → "and" logical-and ;               
equality       → comparison ( ( "!=" | "==" ) comparison )*
               | equal-error ;
equal-error    → ( "!=" | "==" ) equality ;
comparison     → addition ( ( ">" | ">=" | "<" | "<=" ) addition )*
               | compare-error ;
compare-error  → ( ">" | ">=" | "<" | "<=" ) comparison ;
addition       → multiplication ( ( "-" | "+" ) multiplication )*
               | add-error ;
add-error      → ( "+" ) addition ;
multiplication → unary ( ( "/" | "*" ) unary )* 
               | multply-error ;
multiply-error → ( "/" | "*" ) multiplication ;
unary          → ( "!" | "-" ) unary
               | call ;
call           → primary ( "(" arguments? ")" )* ;
arguments      → expression ( "," expression )* ;
primary        → NUMBER | STRING | boolean | "nil"
               | "(" expression ")"
               | IDENTIFIER ;
